#!/bin/tcsh -f

# CMSC 216, Fall 2017, Project #11
# Public test 17 (public17)
#
# Runs xargs.x in line-at-a-time mode to test that when an invocation of the
# target program had a nonzero exit status no further runs of the target
# program are performed.
#
# (c) Larry Herman, 2017.  You are allowed to use this code yourself, but
# not to provide it to anyone else.

# Remove these files if they already exist (because this test could be run
# more than once, and the program might have a bug and not remove them).
# Note that "-e" tests whether a file exists in tcsh.
if ((-e public17.file1) || (-e public17.file2) || (-e public17.file3) || \
    (-e public17.file4)) then
  rm -f public17.file?
endif

# now create files with three of these names
touch public17.file1 public17.file2 public17.file4

# Note we are not even examining the output that is produced (">& /dev/null
# throws it away); in this test we are only concerned with the exit status
# of xargs.x.  Note that the exit status of rm is 1 if some files given on
# its command line didn't exist or couldn't be removed, which is the case
# here.
./xargs.x -i rm < public17.input >& /dev/null

# xargs.x should have returned 1, and NOT removed public17.file4
if ($status == 1 && (-e public17.file4)) then
  # normal exit
  echo "Success!  xargs.x exited with status 1 and public17.file4 is still"
  echo "present."
  exit 0
else
  # error exit
  echo "Problem!  xargs.x did not exit with status 1, or public17.file4 was"
  echo "incorrectly removed."
endif
